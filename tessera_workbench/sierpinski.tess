let square = tile {
    regular_polygon(4)
} with {
    north, west, south, east is this.edges;
    color is "powderblue";
};

let sierpinski_carpet = patch( n ) {
    lay N, NE, E, SE, S, SW, W, NW such_that 
       N.east <-> NE.west,
       NE.south <-> E.north,
       E.south <-> SE.north,
       SE.west <-> S.east,
       S.west <-> SW.east,
       SW.north <-> W.south,
       W.north <-> NW.south
} where {
    let N, NE, E, SE, S, SW, W, NW = 
        if (n == 1) 
            [ square for i in [1 ... 8] ]
        else
            [ sierpinski_carpet(n-1) for i in [1 ... 8] ];
} with {
    north, west, south, east on [N.north, W.west, S.south, E.east];
};

tableau( num ) {
    lay sierpinski_carpet( num )
}