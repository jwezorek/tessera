/* 
   ---------------------
	  Penrose rhombs
   ---------------------
*/


let half_thin_rhombus = func( n ) {
	if (n == 1)
		{ isosceles_triangle( pi/5 ) } with { 
			color is "palegoldenrod"; 
			base, b, a is this.edges; 
			base.broken is true;
		}
	else
		lay flip( half_fat_rhombus(n-1) ), half_thin_rhombus(n-1) 
			such_that $1.a <-> $2.b with { 
				a on $1.base; 
				b on $1.b; 
				base on $2.a; 
			}
};

let half_fat_rhombus = func( n ) {
	if (n==1)
		{ isosceles_triangle( 3*pi/5 )} with { 
			color is "skyblue"; 
			base, b, a is this.edges; 
			base.broken is true; 
		}
	else
		lay flip(half_fat_rhombus(n-1) ), half_thin_rhombus(n-1), half_fat_rhombus(n-1) 
			such_that  $2.b <-> $1.a, $2.a <-> $3.b with {
				a on $1.b; 
				b on $3.base; 
				base on $1.base;
			}
};

let decagon = func(n) {
	lay kite, kite, kite, kite, kite such_that 
		$1.rhs <-> $2.lhs,  $2.rhs <-> $3.lhs,  $3.rhs <-> $4.lhs, $4.rhs <-> $5.lhs
} where {
	let kite =  lay half_thin_rhombus(n), flip( half_thin_rhombus(n) ) 
		such_that 
			$1.a <-> $2.a 
		with { 
			lhs on $1.b; 
			rhs on $2.b; 
		};
};

tableau (n) {
    lay decagon(n)
}