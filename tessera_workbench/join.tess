let square = func {
	regular_polygon(4)
} with {
   N, W, S, E is this.edges;
};

let plus_sign = func {
	join north, west, south, east, center such_that 
		center.N <-> north.S, 
		center.W <-> west.E, 
		center.S <-> south.N, 
		center.E <-> east.W
} where {
	let center = square;
	let north, west, south, east = [square, square, square, square];
} with {
	N, W, S, E on [ north.N , west.W, south.S, east.E];
	NW, SW, SE, NE on [north.W, west.S, south.E, east.N];
    color is "red";
};

let plus_sign_fractal = func(n) {
	lay north, west, south, east, center such_that 
		center.N <-> north.SW, 
		center.W <-> west.SE, 
		center.S <-> south.NE, 
		center.E <-> east.NW
} where {
	 let north, west, south, east, center = 
		 if (n == 1)
			[ plus_sign, plus_sign, plus_sign, plus_sign, plus_sign ]
		 else
			[ plus_sign_fractal(n-1), plus_sign_fractal(n-1), plus_sign_fractal(n-1), plus_sign_fractal(n-1), plus_sign_fractal(n-1) ];
} with {
	N, W, S, E on [ north.N, west.W, south.S, east.E ];
	NW, SW, SE, NE on [ west.NW, south.SW, east.SE, north.NE ];
};

tableau( n ) {
	lay plus_sign_fractal(n)
}